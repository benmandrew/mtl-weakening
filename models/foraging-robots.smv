MODULE main

DEFINE
  TIME_R := 5;
  TIME_D := 5;
  MAX_TIMER := 10;

VAR
  state : { resting, leavingHome, randomWalk, moveToFood, scanArena, grabFood, moveToHome, deposit, homing };
  timer : 0..MAX_TIMER; -- Should be the maximum time that any transition can take

DEFINE
  resting_p :=     state = resting;
  leavingHome_p := state = leavingHome;
  randomWalk_p :=  state = randomWalk;
  moveToFood_p :=  state = moveToFood;
  scanArena_p :=   state = scanArena;
  grabFood_p :=    state = grabFood;
  moveToHome_p :=  state = moveToHome;
  deposit_p :=     state = deposit;
  homing_p :=      state = homing;
  searching_p :=   leavingHome_p | randomWalk_p | moveToFood_p | scanArena_p;

ASSIGN
  init(state) := resting;
  next(state) :=
    case
      state = resting & timer < TIME_R : { resting, leavingHome };
      state = resting & timer = TIME_R : leavingHome;

      state = leavingHome & timer = 2 : {leavingHome, randomWalk };
      state = leavingHome & timer = 3 : randomWalk;

      state = randomWalk : { randomWalk, homing, moveToFood };

      state = moveToFood : { moveToFood, grabFood, homing, scanArena };

      state = scanArena & timer >= 1 & timer < 5 : { scanArena, homing, moveToFood, randomWalk };
      state = scanArena & timer = 5 : { homing, moveToFood, randomWalk };

      state = grabFood & timer = 2 : { grabFood, moveToHome };
      state = grabFood & timer = 3 : moveToHome;

      state = moveToHome & timer < TIME_D : { moveToHome, deposit };
      state = moveToHome & timer = TIME_D : deposit;

      state = deposit & timer = 3 : { deposit, resting };
      state = deposit & timer = 4 : resting;

      state = homing & timer < TIME_D : { homing, resting };
      state = homing & timer = TIME_D : resting;

      TRUE : state;
    esac;

  init(timer) := 1;
  next(timer) :=
    case
      next(state) != state : 1;      -- Reset timer on state change
      timer < MAX_TIMER : timer + 1; -- Otherwise increment
      TRUE : timer;                  -- Saturate timer
    esac;
