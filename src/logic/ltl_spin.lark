start: formula

?formula: implication

?implication: disjunction
            | disjunction "->" implication -> implies

?disjunction: conjunction
            | disjunction "||" conjunction -> disjunction

?conjunction: until
            | conjunction "&&" until -> conjunction

?until: release
      | until "U" release -> until

?release: unary
      | release "V" unary -> release

?unary: "!" unary -> neg
      | "[]" unary -> always
      | "<>" unary -> eventually
      | "X" unary -> next
      | atom
      | "(" formula ")"

atom: "true" -> true
      | "false" -> false
      | CNAME -> ap

%import common.CNAME
%import common.INT
%import common.WS
%ignore WS
